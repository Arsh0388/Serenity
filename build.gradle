plugins {
    id "net.serenity-bdd.serenity-gradle-plugin" version "4.1.12"
    id "java"
}

defaultTasks "clean", "test", "aggregate"

repositories { mavenCentral() }

java {
    toolchain { languageVersion = JavaLanguageVersion.of(17) } // stable LTS for Serenity
}

ext {
    serenityCoreVersion = "4.1.12"
    junitVersion        = "5.10.2"
    assertJVersion      = "3.25.3"
    logbackVersion      = "1.2.11"
}

dependencies {
    testImplementation "net.serenity-bdd:serenity-core:${serenityCoreVersion}",
            "net.serenity-bdd:serenity-junit5:${serenityCoreVersion}",
            "net.serenity-bdd:serenity-screenplay:${serenityCoreVersion}",
            "net.serenity-bdd:serenity-screenplay-webdriver:${serenityCoreVersion}",
            "net.serenity-bdd:serenity-ensure:${serenityCoreVersion}",
            "org.junit.jupiter:junit-jupiter-api:${junitVersion}",
            "org.junit.jupiter:junit-jupiter-engine:${junitVersion}",
            "org.assertj:assertj-core:${assertJVersion}",
            "ch.qos.logback:logback-classic:${logbackVersion}",

            // API testing
            "io.rest-assured:rest-assured:5.5.5",
            "io.rest-assured:json-schema-validator:5.5.0"
    // DO NOT add selenium-java explicitly; let Serenity manage it
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    systemProperties System.getProperties()
}

gradle.startParameter.continueOnFailure = true

serenity {
    reports = ["single-page-html"]
    testRoot = "todomvc"
}

tasks.test.finalizedBy(tasks.aggregate)
